import com.k_int.web.toolkit.custprops.CustomProperty
import com.k_int.web.toolkit.custprops.types.CustomPropertyContainer
import groovy.transform.*
import org.grails.core.artefact.DomainClassArtefactHandler

@Field CustomPropertyContainer customPropertyContainer

// Build up a map for easy sorting.
Map<String, Set<CustomProperty>> propMap = [:]  
for (CustomProperty p : customPropertyContainer.value) {
  final String pName = p.definition.name
  if (!propMap.containsKey(pName)) {
    propMap[pName] = [p] as Set
  } else {
    propMap[pName] << p
  }
}

json {
  propMap.each { final String propName, Set<CustomProperty> props ->
    call propName, props, { CustomProperty p ->
      g.inline( p , [excludes: ['definition', 'parent', 'value'] ]) {
        if (DomainClassArtefactHandler.isDomainClass(p.value.class)) {
          value g.render (p.value, [excludes: ['owner'] ])
        } else {
          value p.value
        }
      }
    } 
  }
}